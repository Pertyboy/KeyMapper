apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 29
    buildToolsVersion = '29.0.3'

    defaultConfig {
        applicationId "io.github.sds100.keymapper"
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 31
        versionName "2.0.1"

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        ci {
            debuggable = true

            applicationIdSuffix ".ci"
            versionNameSuffix "-ci.$RUN_NUMBER"
            signingConfig signingConfigs.debug
        }
    }

    dataBinding {
        enabled = true
        enabledForTests = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // work-runtime-ktx 2.1.0 and above now requires Java 8
    kotlinOptions {
        jvmTarget = "1.8"
    }

    kapt {
        correctErrorTypes = true
    }
}

android.sourceSets {
    test {
        java.srcDirs += "$projectDir/src/testShared"
    }

    androidTest {
        java.srcDirs += "$projectDir/src/testShared"
        assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "keymapper-${variant.versionName}.apk"
    }
}

dependencies {
    def room_version = "2.2.5"
    def coroutine_version = "1.3.4"
    def nav_version = '2.3.0'
    def work_version = "2.3.4"
    def epoxy_version = "3.9.0"
    def splitties_version = "3.0.0-alpha06"
    def markwonVersion = "4.3.1"

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // kotlin stuff
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // random stuff
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    implementation 'com.github.salomonbrys.kotson:kotson:2.5.0'
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'
    implementation 'com.heinrichreimersoftware:material-intro:2.0.0'
    implementation "com.airbnb.android:epoxy:$epoxy_version"
    implementation "com.airbnb.android:epoxy-databinding:$epoxy_version"
    kapt "com.airbnb.android:epoxy-processor:$epoxy_version"
    implementation "io.noties.markwon:core:$markwonVersion"
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // splitties
    implementation "com.louiscad.splitties:splitties-bitflags:$splitties_version"
    implementation "com.louiscad.splitties:splitties-alertdialog-appcompat-coroutines:$splitties_version"
    implementation "com.louiscad.splitties:splitties-alertdialog-appcompat:$splitties_version"
    implementation "com.louiscad.splitties:splitties-preferences:$splitties_version"
    implementation "com.louiscad.splitties:splitties-snackbar:$splitties_version"
    implementation "com.louiscad.splitties:splitties-systemservices:$splitties_version"
    implementation "com.louiscad.splitties:splitties-toast:$splitties_version"

    // androidx
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.0-alpha06'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha04'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    kapt "androidx.room:room-compiler:$room_version"

    def junitVersion = "4.12"
    def androidXTestExtKotlinRunnerVersion = "1.1.1"
    def espressoVersion = "3.2.0"
    def coroutinesVersion = "1.2.1"
    def androidXTestCoreVersion = "1.2.0"

    // Dependencies for local unit tests
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "androidx.test.ext:junit-ktx:$androidXTestExtKotlinRunnerVersion"
    testImplementation "androidx.test:core-ktx:1.2.0"
    testImplementation "org.robolectric:robolectric:4.3.1"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
    testImplementation "pl.pragmatists:JUnitParams:1.1.1"

    androidTestImplementation "androidx.test.ext:junit:$androidXTestExtKotlinRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "org.mockito:mockito-core:3.3.1"
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:2.12.1"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "junit:junit:$junitVersion"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
    androidTestImplementation 'android.arch.persistence.room:testing:1.1.1'
    debugImplementation "androidx.fragment:fragment-testing:1.2.5"
    implementation "androidx.test:core:$androidXTestCoreVersion"
}